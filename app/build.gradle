apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZ", TimeZone.getTimeZone("UTC"))
def gitVersionCode = 'git rev-list --count HEAD'.execute([], project.rootDir).text.trim().toInteger();
def gitBranchName = 'git symbolic-ref -q --short HEAD'.execute([], project.rootDir).text.trim();

def isTravis = "true".equals(System.getenv("TRAVIS"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

// set Geocaching API staging key and secret if production key and secret is not set
// Note: Staging server is slow and not for production use!!!!
if (!project.hasProperty('geocachingApiKey')) {
    project.setProperty('geocachingApiKey', '9C7552E1-3C04-4D04-A395-230D8931E494')
}
if (!project.hasProperty('geocachingApiSecret')) {
    project.setProperty('geocachingApiSecret', 'DA7CC147-7B5B-4423-BCB4-D0C03E2BF685')
}
if (!project.hasProperty('geocachingApiStaging')) {
    project.setProperty('geocachingApiStaging', true)
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.0"

    compileSdkVersion 22
    buildToolsVersion "22"

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries preDexEnabled && !isTravis
    }

    defaultConfig {
        applicationId "com.arcao.trackables"
        minSdkVersion 16
        targetSdkVersion 22
        versionName "1.0"

        versionCode gitVersionCode
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        buildConfigField "String", "GEOCACHING_API_KEY", "\"${geocachingApiKey}\""
        buildConfigField "String", "GEOCACHING_API_SECRET", "\"${geocachingApiSecret}\""
        buildConfigField "boolean", "GEOCACHING_API_STAGING", "${geocachingApiStaging}"
    }
    buildTypes {
        debug {
            versionNameSuffix "-${gitBranchName}"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-rules-butterknife.pro'
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'it.neokree:MaterialNavigationDrawer:1.3'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'info.metadude.android:typed-preferences:2.0.0'
    compile 'io.reactivex:rxandroid:0.24.0'

    compile 'com.google.dagger:dagger:2.0-SNAPSHOT'
    apt 'com.google.dagger:dagger-compiler:2.0-SNAPSHOT'
    compile 'javax.annotation:jsr250-api:1.0'

    compile 'org.slf4j:log4j-over-slf4j:1.7.7'
    compile 'com.arcao:slf4j-timber:1.5'

    compile('com.arcao:geocaching-api:1.5.25') {
        exclude group: 'log4j', module: 'log4j' /* Not working in Android, log4j-over-slf4j used instead */
    }
}
